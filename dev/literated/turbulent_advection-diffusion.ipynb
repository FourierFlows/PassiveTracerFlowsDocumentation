{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Advection-diffusion of tracer by a turbulent flow\n",
    "\n",
    "This is an example demonstrating the advection-diffusion of a tracer using a\n",
    "turbulent flow generated by the `GeophysicalFlows.jl` package.\n",
    "\n",
    "## Install dependencies\n",
    "\n",
    "First let's make sure we have all the required packages installed."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "```julia\n",
    "using Pkg\n",
    "pkg.add([\"PassiveTracerFlows\", \"Printf\", \"CairoMakie\", \"JLD2\"])\n",
    "```\n",
    "\n",
    "## Let's begin\n",
    "First load packages needed to run this example."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using PassiveTracerFlows, Printf, CairoMakie, JLD2\n",
    "using Random: seed!"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Choosing a device: CPU or GPU"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "dev = CPU()\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 2
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Setting up a `MultiLayerQG.Problem` to generate a turbulent flow\n",
    "\n",
    "The tubulent flow we use to advect the passive tracer is generated using the\n",
    "[`MultiLayerQG`](https://fourierflows.github.io/GeophysicalFlowsDocumentation/stable/modules/multilayerqg/) module\n",
    "from the [`GeophysicalFlows.jl`](https://fourierflows.github.io/GeophysicalFlowsDocumentation/stable/) package.\n",
    "A more detailed setup of this two layer system is found at the\n",
    "[GeophysicalFlows Documentation](https://fourierflows.github.io/GeophysicalFlowsDocumentation/stable/literated/multilayerqg_2layer/).\n",
    "\n",
    "### Numerical and time stepping parameters for the flow"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "      n = 128            # 2D resolution = n²\n",
    "stepper = \"FilteredRK4\"  # timestepper\n",
    "     dt = 2.5e-3         # timestep\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 3
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Physical parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "L = 2π                   # domain size\n",
    "μ = 5e-2                 # bottom drag\n",
    "β = 5                    # the y-gradient of planetary PV\n",
    "\n",
    "nlayers = 2              # number of layers\n",
    "f₀, g = 1, 1             # Coriolis parameter and gravitational constant\n",
    "H = [0.2, 0.8]           # the rest depths of each layer\n",
    "ρ = [4.0, 5.0]           # the density of each layer\n",
    "\n",
    " U = zeros(nlayers)      # the imposed mean zonal flow in each layer\n",
    " U[1] = 1.0\n",
    " U[2] = 0.0\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 4
  },
  {
   "cell_type": "markdown",
   "source": [
    "### `MultiLayerQG.Problem` setup, shortcuts and initial conditions"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "(128, 128)"
     },
     "metadata": {},
     "execution_count": 5
    }
   ],
   "cell_type": "code",
   "source": [
    "MQGprob = MultiLayerQG.Problem(nlayers, dev;\n",
    "                               nx=n, Lx=L, f₀, g, H, ρ, U, μ, β,\n",
    "                               dt, stepper, aliased_fraction=0)\n",
    "\n",
    "nx, ny = MQGprob.grid.nx, MQGprob.grid.ny"
   ],
   "metadata": {},
   "execution_count": 5
  },
  {
   "cell_type": "markdown",
   "source": [
    "Initial conditions"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "seed!(1234) # reset of the random number generator for reproducibility\n",
    "q₀  = 1e-2 * ArrayType(dev)(randn((nx, ny, nlayers)))\n",
    "q₀h = MQGprob.timestepper.filter .* rfft(q₀, (1, 2)) # apply rfft  only in dims=1, 2\n",
    "q₀  = irfft(q₀h, nx, (1, 2))                         # apply irfft only in dims=1, 2\n",
    "\n",
    "MultiLayerQG.set_q!(MQGprob, q₀)\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 6
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Tracer advection-diffusion setup\n",
    "\n",
    "Now that we have a `MultiLayerQG.Problem` setup to generate our turbulent flow, we\n",
    "setup an advection-diffusion simulation. This is done by passing the `MultiLayerQG.Problem`\n",
    "as an argument to `TracerAdvectionDiffusion.Problem` which sets up an advection-diffusion problem\n",
    "with same parameters where applicable. We also need to pass a value for the constant diffusivity `κ`,\n",
    "the `stepper` used to step the problem forward and when we want the tracer released into the flow.\n",
    "We let the flow evolve up to `t = tracer_release_time` and then release the tracer and let it\n",
    "evolve with the flow."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[ Info: Stepping the flow forward until t = tracer_release_time = 25.0\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Problem\n  ├─────────── grid: grid (on CPU)\n  ├───── parameters: params\n  ├────── variables: vars\n  ├─── state vector: sol\n  ├─────── equation: eqn\n  ├────────── clock: clock\n  └──── timestepper: FilteredRK4TimeStepper"
     },
     "metadata": {},
     "execution_count": 7
    }
   ],
   "cell_type": "code",
   "source": [
    "κ = 0.002                        # constant diffusivity\n",
    "nsteps = 4000                    # total number of time-steps\n",
    "tracer_release_time = 25.0       # run flow for some time before releasing tracer\n",
    "\n",
    "ADprob = TracerAdvectionDiffusion.Problem(dev, MQGprob; κ, stepper, tracer_release_time)"
   ],
   "metadata": {},
   "execution_count": 7
  },
  {
   "cell_type": "markdown",
   "source": [
    "Some shortcuts for the advection-diffusion problem:"
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "(-3.141592653589793:0.04908738521234052:3.0925052683774528, -3.141592653589793:0.04908738521234052:3.0925052683774528)"
     },
     "metadata": {},
     "execution_count": 8
    }
   ],
   "cell_type": "code",
   "source": [
    "sol, clock, vars, params, grid = ADprob.sol, ADprob.clock, ADprob.vars, ADprob.params, ADprob.grid\n",
    "x, y = grid.x, grid.y"
   ],
   "metadata": {},
   "execution_count": 8
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Initial condition for concentration in both layers\n",
    "\n",
    "We have a two layer system so we advect-diffuse the tracer in both layers.\n",
    "To do this we set the initial condition for tracer concetration as a Gaussian centered at the origin.\n",
    "Then we create some shortcuts for the `TracerAdvectionDiffusion.Problem`."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "gaussian(x, y, σ) = exp(-(x^2 + y^2) / 2σ^2)\n",
    "\n",
    "amplitude, spread = 10, 0.15\n",
    "c₀ = [amplitude * gaussian(x[i], y[j], spread) for j=1:grid.ny, i=1:grid.nx]\n",
    "\n",
    "TracerAdvectionDiffusion.set_c!(ADprob, c₀)"
   ],
   "metadata": {},
   "execution_count": 9
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Saving output\n",
    "\n",
    "The parent package `FourierFlows.jl` provides the functionality to save the output from our simulation.\n",
    "To do this we write a function `get_concentration` and pass this to the `Output` constructor along\n",
    "with the `TracerAdvectionDiffusion.Problem` and the name of the output file."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "Output\n  ├──── prob: FourierFlows.Problem{DataType, Array{ComplexF64, 3}, Float64, Array{Float64, 3}}\n  ├──── path: advection-diffusion.jld2\n  └── fields: Dict{Symbol, Function}(:streamfunction => Main.##286.get_streamfunction, :concentration => Main.##286.get_concentration)"
     },
     "metadata": {},
     "execution_count": 10
    }
   ],
   "cell_type": "code",
   "source": [
    "function get_concentration(prob)\n",
    "  invtransform!(prob.vars.c, deepcopy(prob.sol), prob.params.MQGprob.params)\n",
    "\n",
    "  return prob.vars.c\n",
    "end\n",
    "\n",
    "function get_streamfunction(prob)\n",
    "  params, vars, grid = prob.params.MQGprob.params, prob.params.MQGprob.vars, prob.grid\n",
    "\n",
    "  @. vars.qh = prob.params.MQGprob.sol\n",
    "\n",
    "  streamfunctionfrompv!(vars.ψh, vars.qh, params, grid)\n",
    "\n",
    "  invtransform!(vars.ψ, vars.ψh, params)\n",
    "\n",
    "  return vars.ψ\n",
    "end\n",
    "\n",
    "output = Output(ADprob, \"advection-diffusion.jld2\",\n",
    "                (:concentration, get_concentration), (:streamfunction, get_streamfunction))"
   ],
   "metadata": {},
   "execution_count": 10
  },
  {
   "cell_type": "markdown",
   "source": [
    "This saves information that we will use for plotting later on."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "saveproblem(output)"
   ],
   "metadata": {},
   "execution_count": 11
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Step the problem forward and save the output\n",
    "\n",
    "We specify that we would like to save the concentration every `save_frequency` timesteps;\n",
    "then we step the problem forward."
   ],
   "metadata": {}
  },
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Output saved, step: 0000, t: 0.00, walltime: 0.01 min\n",
      "Output saved, step: 0050, t: 0.13, walltime: 0.03 min\n",
      "Output saved, step: 0100, t: 0.25, walltime: 0.04 min\n",
      "Output saved, step: 0150, t: 0.38, walltime: 0.04 min\n",
      "Output saved, step: 0200, t: 0.50, walltime: 0.05 min\n",
      "Output saved, step: 0250, t: 0.62, walltime: 0.06 min\n",
      "Output saved, step: 0300, t: 0.75, walltime: 0.07 min\n",
      "Output saved, step: 0350, t: 0.87, walltime: 0.08 min\n",
      "Output saved, step: 0400, t: 1.00, walltime: 0.09 min\n",
      "Output saved, step: 0450, t: 1.12, walltime: 0.10 min\n",
      "Output saved, step: 0500, t: 1.25, walltime: 0.10 min\n",
      "Output saved, step: 0550, t: 1.37, walltime: 0.11 min\n",
      "Output saved, step: 0600, t: 1.50, walltime: 0.12 min\n",
      "Output saved, step: 0650, t: 1.62, walltime: 0.13 min\n",
      "Output saved, step: 0700, t: 1.75, walltime: 0.14 min\n",
      "Output saved, step: 0750, t: 1.87, walltime: 0.15 min\n",
      "Output saved, step: 0800, t: 2.00, walltime: 0.15 min\n",
      "Output saved, step: 0850, t: 2.12, walltime: 0.16 min\n",
      "Output saved, step: 0900, t: 2.25, walltime: 0.17 min\n",
      "Output saved, step: 0950, t: 2.37, walltime: 0.18 min\n",
      "Output saved, step: 1000, t: 2.50, walltime: 0.19 min\n",
      "Output saved, step: 1050, t: 2.62, walltime: 0.20 min\n",
      "Output saved, step: 1100, t: 2.75, walltime: 0.20 min\n",
      "Output saved, step: 1150, t: 2.87, walltime: 0.21 min\n",
      "Output saved, step: 1200, t: 3.00, walltime: 0.22 min\n",
      "Output saved, step: 1250, t: 3.12, walltime: 0.23 min\n",
      "Output saved, step: 1300, t: 3.25, walltime: 0.24 min\n",
      "Output saved, step: 1350, t: 3.37, walltime: 0.25 min\n",
      "Output saved, step: 1400, t: 3.50, walltime: 0.25 min\n",
      "Output saved, step: 1450, t: 3.62, walltime: 0.26 min\n",
      "Output saved, step: 1500, t: 3.75, walltime: 0.27 min\n",
      "Output saved, step: 1550, t: 3.87, walltime: 0.28 min\n",
      "Output saved, step: 1600, t: 4.00, walltime: 0.29 min\n",
      "Output saved, step: 1650, t: 4.12, walltime: 0.30 min\n",
      "Output saved, step: 1700, t: 4.25, walltime: 0.30 min\n",
      "Output saved, step: 1750, t: 4.37, walltime: 0.31 min\n",
      "Output saved, step: 1800, t: 4.50, walltime: 0.32 min\n",
      "Output saved, step: 1850, t: 4.63, walltime: 0.33 min\n",
      "Output saved, step: 1900, t: 4.75, walltime: 0.34 min\n",
      "Output saved, step: 1950, t: 4.88, walltime: 0.35 min\n",
      "Output saved, step: 2000, t: 5.00, walltime: 0.35 min\n",
      "Output saved, step: 2050, t: 5.13, walltime: 0.36 min\n",
      "Output saved, step: 2100, t: 5.25, walltime: 0.37 min\n",
      "Output saved, step: 2150, t: 5.38, walltime: 0.38 min\n",
      "Output saved, step: 2200, t: 5.50, walltime: 0.39 min\n",
      "Output saved, step: 2250, t: 5.63, walltime: 0.39 min\n",
      "Output saved, step: 2300, t: 5.75, walltime: 0.40 min\n",
      "Output saved, step: 2350, t: 5.88, walltime: 0.41 min\n",
      "Output saved, step: 2400, t: 6.00, walltime: 0.42 min\n",
      "Output saved, step: 2450, t: 6.13, walltime: 0.43 min\n",
      "Output saved, step: 2500, t: 6.25, walltime: 0.44 min\n",
      "Output saved, step: 2550, t: 6.38, walltime: 0.45 min\n",
      "Output saved, step: 2600, t: 6.50, walltime: 0.45 min\n",
      "Output saved, step: 2650, t: 6.63, walltime: 0.46 min\n",
      "Output saved, step: 2700, t: 6.75, walltime: 0.47 min\n",
      "Output saved, step: 2750, t: 6.88, walltime: 0.48 min\n",
      "Output saved, step: 2800, t: 7.00, walltime: 0.49 min\n",
      "Output saved, step: 2850, t: 7.13, walltime: 0.50 min\n",
      "Output saved, step: 2900, t: 7.25, walltime: 0.50 min\n",
      "Output saved, step: 2950, t: 7.38, walltime: 0.51 min\n",
      "Output saved, step: 3000, t: 7.50, walltime: 0.52 min\n",
      "Output saved, step: 3050, t: 7.63, walltime: 0.53 min\n",
      "Output saved, step: 3100, t: 7.75, walltime: 0.54 min\n",
      "Output saved, step: 3150, t: 7.88, walltime: 0.55 min\n",
      "Output saved, step: 3200, t: 8.00, walltime: 0.56 min\n",
      "Output saved, step: 3250, t: 8.13, walltime: 0.56 min\n",
      "Output saved, step: 3300, t: 8.25, walltime: 0.57 min\n",
      "Output saved, step: 3350, t: 8.38, walltime: 0.58 min\n",
      "Output saved, step: 3400, t: 8.50, walltime: 0.59 min\n",
      "Output saved, step: 3450, t: 8.63, walltime: 0.60 min\n",
      "Output saved, step: 3500, t: 8.75, walltime: 0.60 min\n",
      "Output saved, step: 3550, t: 8.88, walltime: 0.61 min\n",
      "Output saved, step: 3600, t: 9.00, walltime: 0.62 min\n",
      "Output saved, step: 3650, t: 9.13, walltime: 0.63 min\n",
      "Output saved, step: 3700, t: 9.25, walltime: 0.64 min\n",
      "Output saved, step: 3750, t: 9.38, walltime: 0.65 min\n",
      "Output saved, step: 3800, t: 9.50, walltime: 0.65 min\n",
      "Output saved, step: 3850, t: 9.63, walltime: 0.66 min\n",
      "Output saved, step: 3900, t: 9.75, walltime: 0.67 min\n",
      "Output saved, step: 3950, t: 9.88, walltime: 0.68 min\n",
      "Output saved, step: 4000, t: 10.00, walltime: 0.69 min\n"
     ]
    }
   ],
   "cell_type": "code",
   "source": [
    "save_frequency = 50 # frequency at which output is saved\n",
    "\n",
    "startwalltime = time()\n",
    "while clock.step <= nsteps\n",
    "  if clock.step % save_frequency == 0\n",
    "    saveoutput(output)\n",
    "    log = @sprintf(\"Output saved, step: %04d, t: %.2f, walltime: %.2f min\",\n",
    "                   clock.step, clock.t, (time()-startwalltime) / 60)\n",
    "\n",
    "    println(log)\n",
    "  end\n",
    "\n",
    "  stepforward!(ADprob)\n",
    "  stepforward!(params.MQGprob)\n",
    "  MultiLayerQG.updatevars!(params.MQGprob)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": 12
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Visualizing the output\n",
    "\n",
    "We now have output from our simulation saved in `advection-diffusion.jld2`.\n",
    "As a demonstration, we load the JLD2 output and create a time series for the tracer\n",
    "in the lower layer of our fluid along with the flow streamlines."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Create time series for the concentration and streamfunction in the bottom layer, `layer = 2`."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "file = jldopen(output.path)\n",
    "\n",
    "iterations = parse.(Int, keys(file[\"snapshots/t\"]))\n",
    "t = [file[\"snapshots/t/$i\"] for i ∈ iterations]\n",
    "\n",
    "layer = 2\n",
    "\n",
    "c = [file[\"snapshots/concentration/$i\"][:, :, layer] for i ∈ iterations]\n",
    "ψ = [file[\"snapshots/streamfunction/$i\"][:, :, layer] for i ∈ iterations]\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 13
  },
  {
   "cell_type": "markdown",
   "source": [
    "We normalize all streamfunctions to have maximum absolute value 1."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "for i in 1:lastindex(ψ)\n",
    "  ψ[i] /= maximum(abs, ψ[i])\n",
    "end\n",
    "\n",
    "x,  y  = file[\"grid/x\"],  file[\"grid/y\"]\n",
    "Lx, Ly = file[\"grid/Lx\"], file[\"grid/Ly\"]\n",
    "\n",
    "n = Observable(1)\n",
    "\n",
    "c_anim = @lift c[$n]\n",
    "ψ_anim = @lift ψ[$n]\n",
    "title = @lift @sprintf(\"concentration, t = %.2f\", t[$n])\n",
    "\n",
    "fig = Figure(resolution = (600, 600))\n",
    "ax = Axis(fig[1, 1],\n",
    "          xlabel = \"x\",\n",
    "          ylabel = \"y\",\n",
    "          aspect = 1,\n",
    "          title = title,\n",
    "          limits = ((-Lx/2, Lx/2), (-Ly/2, Ly/2)))\n",
    "\n",
    "hm = heatmap!(ax, x, y, c_anim;\n",
    "              colormap = :balance, colorrange = (-1, 1))\n",
    "contour!(ax, x, y, ψ_anim;\n",
    "         levels = 0.1:0.2:1, color = :grey, linestyle = :solid)\n",
    "contour!(ax, x, y, ψ_anim;\n",
    "         levels = -0.1:-0.2:-1, color = (:grey, 0.8), linestyle = :dash)\n",
    "\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 14
  },
  {
   "cell_type": "markdown",
   "source": [
    "Create a movie of the tracer with the streamlines."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "frames = 1:length(t)\n",
    "record(fig, \"turbulentflow_advection-diffusion.mp4\", frames, framerate = 12) do i\n",
    "    n[] = i\n",
    "end\n",
    "\n",
    "nothing # hide"
   ],
   "metadata": {},
   "execution_count": 15
  },
  {
   "cell_type": "markdown",
   "source": [
    "![](turbulentflow_advection-diffusion.mp4)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.7"
  },
  "kernelspec": {
   "name": "julia-1.6",
   "display_name": "Julia 1.6.7",
   "language": "julia"
  }
 },
 "nbformat": 4
}
